#+title: Bonk Modules
#+OPTIONS: toc:t
#+PROPERTY: header-args:emacs-lisp :tangle ./../core/bonk-modules.el :mkdirp yes
* Bonk Modules
** Variables
#+begin_src emacs-lisp

	  ;;; bonk-modules.el --- module & package management system -*- lexical-binding: t; -*-
	  ;;; Commentary:
	  ;;; Code:

  ;;
	  ;;; Variables
  (defvar bonk-core-dir (expand-file-name "core/" bonk-config-path)
	"The root directory for Doom's modules. Must end with a slash.")
  (defvar bonk-modules (make-hash-table :test 'equal)
	"A hash table of enabled modules. Set by `bonk-initialize-modules'.")

  (defvar bonk-core-dirs
	(list (expand-file-name "core/" bonk-config-path)
		  bonk-core-dir)
	"A list of module root directories. Order determines priority.")
#+end_src

#+RESULTS:
: bonk-core-dirs

*** Module file Variables
#+begin_src emacs-lisp
  ;;; Module file variables
  (defvar bonk-module-init-file "init.el"
	"The filename for module early initialization config files.

  Init files are loaded early, just after Bonk core, and before modules' config
  files. They are always loaded, even in non-interactive sessions, and before
  `bonk-before-modules-init-hook'. Related to `bonk-module-config-file'.")

  (defvar bonk-module-config-file "main.el"
	"The filename for module configuration files.

  Config files are loaded later, and almost always in interactive sessions. These
  run before `bonk-after-modules-config-hook' and after `bonk-module-init-file'.")

  (defvar bonk-module-packages-file "packages.el"
	"The filename for the package configuration file.

  Package files are read whenever Bonk's package manager wants a manifest of all
  desired packages. They are rarely read in interactive sessions (unless the user
  uses a straight or package.el command directly).")

  (defvar bonk-inhibit-module-warnings (not noninteractive)
	"If non-nil, don't emit deprecated or missing module warnings at startup.")

#+end_src

#+RESULTS:
: bonk-inhibit-module-warnings
  
** Module hooks 
#+begin_src emacs-lisp
  ;;; Custom hooks
  (defcustom bonk-before-modules-init-hook nil
	"Hooks run before module init.el files are loaded."
	:group 'bonk
	:type 'hook)

  (defcustom bonk-after-modules-init-hook nil
	"Hooks run after module init.el files are loaded."
	:group 'bonk
	:type 'hook)

  (defcustom bonk-before-modules-config-hook nil
	"Hooks run before module config.el files are loaded."
	:group 'bonk
	:type 'hook)

  (defcustom bonk-after-modules-config-hook nil
	"Hooks run after module config.el files are loaded (but before the user's)."
	:group 'bonk
	:type 'hook)


#+end_src

#+RESULTS:
: bonk-after-modules-config-hook
  
** Module Context 
#+begin_src emacs-lisp
  ;;
  ;;; `bonk-module-context'

  (defvar bonk--empty-module-context [nil nil nil nil nil nil nil])

  (eval-and-compile
	(setplist 'bonk-module-context '(index 0 initdepth 1 configdepth 2
									 group 3 name 4 flags 5 features 6)))
  (defvar bonk-module-context bonk--empty-module-context
	"A vector describing the module associated it with the active context.

  Contains the following: [INDEX INITDEPTH CONFIGDEPTH :GROUP MODULE FLAGS FEATURES]

  Do not directly set this variable, only let-bind it.")

  ;; DEPRECATED: Remove this when byte-compilation is introduced to Bonk core.
  (defmacro bonk-module--context-field (field) (get 'bonk-module-context field))

  (defun bonk-module-context-get (field &optional context)
	"Return the FIELD of CONTEXT.

  FIELD should be one of `index', `initdepth', `configdepth', `group', `name',
  `flags', or `features'.  CONTEXT should be a `bonk-module-context' vector. If
  omitted, defaults to `bonk-module-context'."
	(aref (or context bonk-module-context) (get 'bonk-module-context field)))

  (defun bonk-module-context (group &optional name)
	"Create a `bonk-module-context' from a module by GROUP and NAME.

  If NAME is omitted, GROUP is treated as a module key cons cell: (GROUP . NAME)."
	(declare (side-effect-free t))
	(let ((key (if name (cons group name) group)))
	  (or (get (or (car-safe key) key)
			   (cdr-safe key))
		  bonk--empty-module-context)))

  (defun bonk-module-context-key (&optional context)
	"Return the module of the active `bonk-module-context' as a module key."
	(declare (side-effect-free t))
	(let ((context (or context bonk-module-context)))
	  (cons (aref context (bonk-module--context-field group))
			(aref context (bonk-module--context-field name)))))

  (defmacro bonk-module-context-with (module-key &rest body)
	"Evaluate BODY with `bonk-module-context' informed by MODULE-KEY."
	(declare (indent 1))
	`(let ((bonk-module-context (bonk-module-context ,module-key)))
	   (bonk-log ":context:module: =%s" bonk-module-context)
	   ,@body))


#+end_src

#+RESULTS:
: bonk-module-context-with
  
** Module API
#+begin_src emacs-lisp
  ;;
  ;;; Module API

  (defun bonk-module-p (category module &optional flag)
	"Returns t if CATEGORY MODULE is enabled (ie. present in `bonk-modules')."
	(declare (pure t) (side-effect-free t))
	(when-let (plist (gethash (cons category module) bonk-modules))
	  (or (null flag)
		  (and (memq flag (plist-get plist :flags))
			   t))))

  (defun bonk-module-depth (category module &optional initdepth?)
	"Return the depth of CATEGORY MODULE.

  If INITDEPTH? is non-nil, use the CAR if a module was given two depths (see
  `bonk-module-set')."
	(if-let (depth (bonk-module-get category module :depth))
		(or (if initdepth?
				(car-safe depth)
			  (cdr-safe depth))
			depth)
	  0))

  (defun bonk-module-get (category module &optional property)
	"Returns the plist for CATEGORY MODULE. Gets PROPERTY, specifically, if set."
	(declare (pure t) (side-effect-free t))
	(when-let (plist (gethash (cons category module) bonk-modules))
	  (if property
		  (plist-get plist property)
		plist)))

  (defun bonk-module-put (category module &rest plist)
	"Set a PROPERTY for CATEGORY MODULE to VALUE. PLIST should be additional pairs
  of PROPERTY and VALUEs.

  \(fn CATEGORY MODULE PROPERTY VALUE &rest [PROPERTY VALUE [...]])"
	(puthash (cons category module)
			 (if-let (old-plist (bonk-module-get category module))
				 (if (null plist)
					 old-plist
				   (when (cl-oddp (length plist))
					 (signal 'wrong-number-of-arguments (list (length plist))))
				   (while plist
					 (plist-put old-plist (pop plist) (pop plist)))
				   old-plist)
			   plist)
			 bonk-modules))

  (defun bonk-module-set (category module &rest plist)
	"Enables a module by adding it to `bonk-modules'.

  CATEGORY is a keyword, module is a symbol, PLIST is a plist that accepts the
  following properties:

	:path STRING
	  Path to the directory where this module lives.
	:depth INT|(INITDEPTH . CONFIGDEPTH)
	  Determines module load order. If a cons cell, INITDEPTH determines the load
	  order of the module's init.el, while CONFIGDEPTH determines the same for all
	  other config files (config.el, packages.el, doctor.el, etc).
	:flags (SYMBOL...)
	  A list of activated flags for this module.
	:features (SYMBOL...)
	  A list of active features, determined from module's metadata. NOT
	  IMPLEMENTED YET.

  If PLIST consists of a single nil, the module is purged from memory instead."
	(if (car plist)
		(let* ((depth (ensure-list (or (plist-get plist :depth) 0)))
			   (idepth (or (cdr depth) (car depth)))
			   (cdepth (car depth))
			   (idx (hash-table-count bonk-modules)))
		  ;; PERF: Bonk caches module index, flags, and features in symbol plists
		  ;;   for fast lookups in `modulep!' and elsewhere. plists are lighter
		  ;;   and faster than hash tables for datasets this size, and this
		  ;;   information is looked up *very* often. The structure of this cache
		  ;;   should match `bonk-module-context's.
		  (put category module
			   (vector idx idepth cdepth
					   category module
					   (plist-get plist :flags)
					   (plist-get plist :features)))
		  ;; The hash table will always been Bonk's formal storage for
		  ;; modules.
		  (puthash (cons category module) plist bonk-modules))
	  (remhash (cons category module) bonk-modules)
	  (cl-remf (symbol-plist category) module)))

  (defun bonk-module-list (&optional paths-or-all initorder?)
	"Return a list of (:group . name) module keys in order of their :depth.

  PATHS-OR-ALL can either be a non-nil value or a list of directories. If given a
  list of directories, return a list of module keys for all modules present
  underneath it.  If non-nil, return the same, but search `bonk-core-dirs'
  (includes :core and :user). Modules that are enabled are sorted first by their
  :depth, followed by disabled modules in lexicographical order (unless a :depth
  is specified in their .bonkmodule).

  If INITORDER? is non-nil, sort modules by their initdepth, rather than their
  configdepth. See `bonk-module-set' for details."
	(sort (if paths-or-all
			  (delete-dups
			   (append (seq-remove #'cdr (bonk-module-list nil initorder?))
					   (bonk-files-in (if (listp paths-or-all)
										  paths-or-all
										bonk-core-dirs)
									  :map #'bonk-module-from-path
									  :type 'dirs
									  :mindepth 1
									  :depth 1)))
			(hash-table-keys bonk-modules))
		  (let ((idx (if initorder? 1 2)))
			(lambda! ((groupa . namea) (groupb . nameb))
			  (let ((a (get groupa namea))
					(b (get groupb nameb)))
				(or (null b)
					(and
					 a (let ((adepth (aref a idx))
							 (bdepth (aref b idx)))
						 (if (= adepth bdepth)
							 (< (aref a 0) (aref b 0))
						   (< adepth bdepth))))))))))

  (defun bonk-module-expand-path (category module &optional file)
	"Expands a path to FILE relative to CATEGORY and MODULE.

  CATEGORY is a keyword. MODULE is a symbol. FILE is an optional string path.
  If the category isn't enabled this returns nil. For finding disabled modules use
  `bonk-module-locate-path'."
	(when-let (path (bonk-module-get category module :path))
	  (if file
		  (file-name-concat path file)
		path)))

  (defun bonk-module-locate-path (category &optional module file)
	"Searches `bonk-core-dirs' to find the path to a module.

  CATEGORY is a keyword (e.g. :lang) and MODULE is a symbol (e.g. 'python). FILE
  is a string that will be appended to the resulting path. If no path exists, this
  returns nil, otherwise an absolute path."
	(let (file-name-handler-alist)
	  (if-let (path (bonk-module-expand-path category module file))
		  (if (or (null file)
				  (file-exists-p path))
			  path)
		(let* ((category (bonk-keyword-name category))
			   (module (if module (symbol-name module)))
			   (path (file-name-concat category module file)))
		  (if file
			  ;; PERF: locate-file-internal is a little faster for finding files,
			  ;;   but its interface for finding directories is clumsy.
			  (locate-file-internal path bonk-core-dirs '("" ".elc" ".el"))
			(cl-loop for default-directory in bonk-core-dirs
					 if (file-exists-p path)
					 return (expand-file-name path)))))))

  (defun bonk-module-locate-paths (module-list file)
	"Return all existing paths to FILE under each module in MODULE-LIST.

  MODULE-LIST is a list of cons cells (GROUP . NAME). See `bonk-module-list' for
  an example."
	(cl-loop with file = (file-name-sans-extension file)
			 for (group . name) in module-list
			 if (bonk-module-locate-path group name file)
			 collect it))

  (defun bonk-module-from-path (path &optional enabled-only)
	"Returns a cons cell (CATEGORY . MODULE) derived from PATH (a file path).
  If ENABLED-ONLY, return nil if the containing module isn't enabled."
	(let* ((file-name-handler-alist nil)
		   (path (expand-file-name path)))
	  (save-match-data
		(cond ((string-match "/modules/\\([^/]+\\)/\\([^/]+\\)\\(?:/.*\\)?$" path)
			   (when-let* ((category (bonk-keyword-intern (match-string 1 path)))
						   (module   (intern (match-string 2 path))))
				 (and (or (null enabled-only)
						  (bonk-module-p category module))
					  (cons category module))))
			  ((file-in-directory-p path bonk-core-dir)
			   (cons :core nil))
			  ))))

  (defun bonk-module-load-path (&optional module-dirs)
	"Return a list of file paths to activated modules.

  The list is in no particular order and its file paths are absolute. If
  MODULE-DIRS is non-nil, include all modules (even disabled ones) available in
  those directories."
	(declare (pure t) (side-effect-free t))
	(cl-loop for (cat . mod) in (bonk-module-list module-dirs)
			 collect (bonk-module-locate-path cat mod)))

  (defun bonk-module-mplist-map (fn mplist)
	"Apply FN to each module in MPLIST."
	(let ((mplist (copy-sequence mplist))
		  (inhibit-message bonk-inhibit-module-warnings)
		  obsolete
		  results
		  category m)
	  (while mplist
		(setq m (pop mplist))
		(cond ((keywordp m)
			   (setq category m
					 obsolete (assq m bonk-obsolete-modules)))
			  ((null category)
			   (error "No module category specified for %s" m))
			  ((and (listp m) (keywordp (car m)))
			   (pcase (car m)
				 (:cond
				  (cl-loop for (cond . mods) in (cdr m)
						   if (eval cond t)
						   return (prependq! mplist mods)))
				 (:if (if (eval (cadr m) t)
						  (push (caddr m) mplist)
						(prependq! mplist (cdddr m))))
				 (test (if (xor (eval (cadr m) t)
								(eq test :unless))
						   (prependq! mplist (cddr m))))))
			  ((catch 'bonk-modules
				 (let* ((module (if (listp m) (car m) m))
						(flags  (if (listp m) (cdr m))))
				   (when-let (new (assq module obsolete))
					 (let ((newkeys (cdr new)))
					   (if (null newkeys)
						   (print! (warn "%s module was removed"))
						 (if (cdr newkeys)
							 (print! (warn "%s module was removed and split into the %s modules")
									 (list category module)
									 (mapconcat #'prin1-to-string newkeys ", "))
						   (print! (warn "%s module was moved to %s")
								   (list category module)
								   (car newkeys)))
						 (push category mplist)
						 (dolist (key newkeys)
						   (push (if flags
									 (nconc (cdr key) flags)
								   (cdr key))
								 mplist)
						   (push (car key) mplist))
						 (throw 'bonk-modules t))))
				   (push (funcall fn category module :flags (if (listp m) (cdr m)))
						 results))))))
	  (when noninteractive
		(setq bonk-inhibit-module-warnings t))
	  (nreverse results)))
#+end_src

#+RESULTS:
: bonk-module-mplist-map

** TODO Module config macros

#+begin_src emacs-lisp
  ;;
  ;;; Module config macros

  (put :if     'lisp-indent-function 2)
  (put :when   'lisp-indent-function 'defun)
  (put :unless 'lisp-indent-function 'defun)

  (defmacro bonk! (&rest modules)
	"Bootstraps BONK Emacs and its modules.

  If the first item in MODULES doesn't satisfy `keywordp', MODULES is evaluated,
  otherwise, MODULES is a multiple-property list (a plist where each key can have
  multiple, linear values).

  The bootstrap process involves making sure the essential directories exist, core
  packages are installed, `bonk-autoloads-file' is loaded, `bonk-packages-file'
  cache exists (and is loaded) and, finally, loads your private init.el (which
  should contain your `bonk!' block).

  Module load order is determined by your `bonk!' block. See `bonk-core-dirs'
  for a list of all recognized module trees. Order defines precedence (from most
  to least)."
	`(when noninteractive
	   (bonk-module-mplist-map
		(lambda (category module &rest plist)
		  (let ((path (bonk-module-locate-path category module)))
			(unless path
			  (print! (warn "Failed to locate a '%s %s' module") category module))
			(apply #'bonk-module-set category module
				   :path path
				   plist)))
		,@(if (keywordp (car modules))
			  (list (list 'quote modules))
			modules))
	   bonk-modules))
  


#+end_src

#+RESULTS:
: bonk!

** modulep!
#+begin_src emacs-lisp

(defmacro modulep! (category &optional module flag)
  "Return t if :CATEGORY MODULE (and +FLAGS) are enabled.

If FLAG is provided, returns t if CATEGORY MODULE has FLAG enabled.

  (modulep! :config default +flag)

CATEGORY and MODULE may be omitted when this macro is used from a Bonk module's
source (except your BONKDIR, which is a special module). Like so:

  (modulep! +flag)

For more about modules and flags, see `bonk!'."
  ;; PERF: This macro bypasses the module API to spare startup their runtime
  ;;   cost, as `modulep!' gets called *a lot* during startup. In the future,
  ;;   Bonk will byte-compile its core files. At that time, we can use it again.
  (and (cond (flag (memq flag (aref (or (get category module) bonk--empty-module-context)
                                    (bonk-module--context-field flags))))
             (module (get category module))
             ((aref bonk-module-context 0)
              (memq category (aref bonk-module-context
                                   (bonk-module--context-field flags))))
             ((let ((file
                     ;; This must be expanded at the call site, not in
                     ;; `modulep!'s definition, to get the file we want.
                     (macroexpand '(file!))))
                (if-let (module (bonk-module-from-path file))
                    (memq category (aref (or (get (car module) (cdr module))
                                             bonk--empty-module-context)
                                         (bonk-module--context-field flags)))
                  (error "(modulep! %s %s %s) couldn't figure out what module it was called from (in %s)"
                         category module flag file)))))
       t))
#+end_src

#+RESULTS:
: modulep!

** defaults

#+begin_src emacs-lisp
 
 ;;
  ;;; Defaults

  ;; Register Bonk's two virtual module categories, representing Bonk's core and
  ;; the user's config; which are always enabled.
  (bonk-module-set :core nil :path bonk-core-dir :depth -110)

  ;; DEPRECATED: I intend to phase out our internal usage of `use-package' and
  ;;   move it to a :config use-package module. The macro is far too complex and
  ;;   magical for our needs, but until this move is done, ':config use-package'
  ;;   will remain a hardcoded module for backwards compatibility.

  (provide 'bonk-modules)
#+end_src

#+RESULTS:
: bonk-modules
